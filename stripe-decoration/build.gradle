apply plugin: 'com.android.library'
//apply plugin: 'jacoco-android'

version = "1.0.1"
group = 'com.sanbeg.sdk'

android {
    compileSdkVersion 29
    //buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 29
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {

    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.19.0'
}

apply plugin: 'com.github.dcendents.android-maven'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId project.group
                artifactId project.name
                version project.version

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += project.files("build/intermediates/exploded-aar/com.android.support/recyclerview-v7/25.2.0/jars/classes.jar")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    //archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
try {
    properties.load(project.rootProject.file('bintray.properties').newDataInputStream())
}
catch (IOException e) {
    e.println()
}

bintray {
    user = properties.getProperty("user")
    key = properties.getProperty("key")

    configurations = ['archives']

    pkg {
        repo = "maven"
        name = project.name
        version {
            name = project.version
        }
        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/sanbeg/StripeDecoration"
        publish = false
    }
}
